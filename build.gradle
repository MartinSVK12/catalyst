plugins {
	id 'babric-loom' version '1.4.+'
	id 'java'
}

group = project.core_mod_group
archivesBaseName = project.core_mod_name
version = project.core_mod_version

loom {
	gluedMinecraftJar()
	noIntermediateMappings()
	customMinecraftManifest.set("https://github.com/Turnip-Labs/bta-manifest-repo/releases/download/v${project.bta_version}/${project.bta_version}.json")
}

allprojects {
	repositories {
		mavenCentral()
		maven { url = "https://jitpack.io" }
		maven {
			name = 'Babric'
			url = 'https://maven.glass-launcher.net/babric'
		}
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		ivy {
			url = "https://github.com/Better-than-Adventure"
			patternLayout {
				artifact "[organisation]/releases/download/v[revision]/[module].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://github.com/Turnip-Labs"
			patternLayout {
				artifact "[organisation]/releases/download/v[revision]/[module]-[revision].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://github.com/Turnip-Labs"
			patternLayout {
				artifact "[organisation]/releases/download/[revision]/[module]-[revision].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://github.com/Turnip-Labs"
			patternLayout {
				artifact "[organisation]/releases/download/[revision]/[module]-bta-[revision].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://piston-data.mojang.com"
			patternLayout {
				artifact "v1/[organisation]/[revision]/[module].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://github.com/MartinSVK12"
			patternLayout {
				artifact "[organisation]/releases/download/[revision]/[module]-[revision].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = "https://github.com/UselessBullets"
			patternLayout {
				artifact "[organisation]/releases/download/[revision]/[module]-bta-[revision].jar"
				m2compatible = true
			}
			metadataSources { artifact() }
		}
		ivy {
			url = uri("https://github.com/UselessBullets")
			patternLayout {
				artifact("[organisation]/releases/download/v[revision]/[module]-[revision].jar")
			}
			metadataSources { artifact() }
		}
	}
}

dependencies {
	minecraft "bta-download-repo:bta:${project.bta_version}"
	mappings loom.layered() {}

	implementation "org.slf4j:slf4j-api:1.8.0-beta4"
	implementation "org.apache.logging.log4j:log4j-slf4j18-impl:2.16.0"
	implementation 'com.google.guava:guava:33.0.0-jre'
	implementation group: 'com.google.code.gson', name: 'gson', version: '2.10.1'
	var log4jVersion = "2.20.0"
	implementation("org.apache.logging.log4j:log4j-core:${log4jVersion}")
	implementation("org.apache.logging.log4j:log4j-api:${log4jVersion}")
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:0.4.1")))

	modRuntimeOnly "objects:client:43db9b498cb67058d2e12d394e6507722e71bb45" // https://piston-data.mojang.com/v1/objects/43db9b498cb67058d2e12d394e6507722e71bb45/client.jar
	modImplementation "fabric-loader:fabric-loader:${project.loader_version}"
	modImplementation "io.github.llamalad7:mixinextras-fabric:0.4.1"

	modImplementation "bta-halplibe:halplibe:${project.halplibe_version}"
	modImplementation "ModMenu:ModMenu:${project.mod_menu_version}"

	if(findProject(":catalyst") == null){
		runtimeOnly(project(path: ":energy", "configuration": "namedElements"))
		runtimeOnly(project(path: ":fluids", "configuration": "namedElements"))
		runtimeOnly(project(path: ":multiblocks", "configuration": "namedElements"))
		runtimeOnly(project(path: ":effects", "configuration": "namedElements"))
	}
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.release.set 8
}

remapJar {
	destinationDirectory.set(file("${projectDir}/jars"))
	archiveVersion.set(archiveVersion.get()+"-${bta_version}")
}

